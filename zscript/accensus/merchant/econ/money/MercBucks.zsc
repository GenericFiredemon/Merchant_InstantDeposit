class MercenaryBucks : HDPickup {

    Default {
        Tag "$TAG_BUX";
        Inventory.Icon "MBCKA0";
        Inventory.PickupMessage "$PICKUP_BUX";

        +HDPICKUP.CHEATNOGIVE
        -HDPICKUP.FITSINBACKPACK
        -HDPICKUP.DROPTRANSLATION
        +INVENTORY.UNDROPPABLE
        -INVENTORY.INVBAR
        +HDPICKUP.MULTIPICKUP

        HDPickup.Bulk 0;
        HDPickup.RefID "bux";
        Inventory.MaxAmount 100000;
        scale 0.4;
    }

    override void Tick() {
        Super.Tick();

        A_ChangeFlag("HDPICKUP.FITSINBACKPACK", hdm_moneydroppable);
        A_ChangeFlag("INVENTORY.UNDROPPABLE", !hdm_moneydroppable);
    }

    override double getbulk() {
        return hdm_moneydroppable ? amount * hdm_moneybulk : 0;
    }

    override void SplitPickup() {

        int packnum = 25;
        int boxnum = 100;
        Class<Actor> boxtype = 'MercenaryBucks100';
        name packsprite = 'MBC1A0';
        name singlesprite = 'MBCKA0';

        while (amount > packnum) {
            if (amount >= boxnum) {
                actor aaa = spawn(boxtype, pos + (frandom(-1, 1), frandom(-1, 1), frandom(-1, 1)));
                aaa.vel = vel + (frandom(-0.6, 0.6), frandom(-0.6, 0.6), frandom(-0.6, 0.6));
                aaa.angle = angle;
                amount -= boxnum;
            } else {
                let sss = hdpickup(spawn(getclassname(), pos + (frandom(-1, 1), frandom(-1, 1), frandom(-1, 1))));
                sss.vel = vel+(frandom(-0.6, 0.6), frandom(-0.6, 0.6), frandom(-0.6, 0.6));
                sss.amount = packnum;
                sss.angle = angle;
                amount -= packnum;
            }

            if (amount < 1) {
                destroy();
                return;
            }
        }

        if (amount == packnum) {
            sprite = getspriteindex(packsprite);
        } else {
            super.SplitPickup();
        }
    }

    States {
        PreCache:
            MBC1 A -1;
        Spawn:
            MBCK A -1;
    }
}

class MercenaryBucks100 : HDUPK {

    Default {
        Tag "$TAG_BUX";
        hdupk.pickupsound "weapons/pocket";
        hdupk.pickupmessage "$PICKUP_BUX";
        hdupk.pickuptype "MercenaryBucks";
        hdupk.amount 100;

        +HDUPK.MULTIPICKUP

        scale 0.4;
    }

    States {
        Spawn:
            MBC2 A -1;
    }
}
